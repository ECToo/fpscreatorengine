;//Requires v1.17+ DarkAI
desc          = DarkAI generic shoot and melee with ally command assign to AI as shoot script TEAM 2 ONLY

;//Dark AI Animations----------------------------------------------------------------------------------------------------
;//Running
:varequal=0,activated=1,ducking=0,movingforwards=1,aitargetdistfurther=200:animate=5
:varequal=1,activated=1,ducking=0,movingforwards=1,aitargetdistfurther=200:animate=2
:activated=1,ducking=0,movingforwards=1,aitargetdistwithin=200:animate=2
;//Walking animations
:ducking=0,strafingleft=1:animate=3
:ducking=0,strafingright=1:animate=4
:activated=0,ducking=0,movingforwards=1:animate=2
:ducking=0,movingbackwards=1:animate=2
:ducking=0,idle=1:animate=1
;//Crouching Animations
:ducking=1,idle=0:animate=32
:ducking=1,idle=1:animate=31

;//Common behavior and triggers---------------------------------------------------------------------------------
;//Sprint when activated
:varequal=1,activated=0:aisetspeed=50
:varequal=1,activated=1:aisetspeed=80
:varequal=0,activated=0:aisetspeed=100
:varequal=0,activated=1:aisetspeed=250
:aihastarget=1,movingforwards=1,aitargetdistfurther=100:activate=1
:aihastarget=1,movingforwards=1,aitargetdistwithin=90:activate=0
:movingforwards=0:activate=0
:aihastarget=0:activate=0

;//Targeting
:aiaction=1:aiusefullaim=1
:aiaction=0:aiusefullaim=0
:state=1,aihastarget=0:aiattackawareness=1,aisettarget,airotatetotarget
:aihastarget=1,aicanshoot=0:aiattackawareness=0,setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=1:airotatetotarget

;//Shooting
:aiteam=2,state=1,hasweapon=1,ifweapon=1,aicanshoot=1,idle=1,rateoffire:useweapon,rundecal=6
:aiteam=2,state=1,hasweapon=1,ifweapon=1,aicanshoot=1,idle=0,rateoffire:useweapon,rundecal=6
:aiteam=2,state=5,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6
:aiteam=2,state=55,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6
:aiteam=2,state=67,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6

;//Fall back to cover while returning fire
:state=60:etimerstart,state=67
:state=67,aiatcover=0:aimovetocover=0,airotatetotarget
:state=67,aiatcover=1:activate=0,state=1
:state=67,movingforwards=1,aitargetdistfurther=100,etimergreater=200:activate=1
:state=67,movingforwards=0:activate=0
:state=67,etimergreater=2000:activate=0,state=1

;//close with enemy for melee if seen nearby
:state=1,varequal=1,aicanshoot=1,aitargetdistfurther=70,random=10:etimerstart,state=55
:state=55:aimovetotarget
:state=55,movingforwards=1,aitargetdistwithin=200,etimergreater=300:activate=1
:state=55,movingforwards=0:activate=0
:state=55,aitargetdistwithin=70:activate=0,state=190
:state=55,aicanshoot=0:activate=0,state=1
:state=55,aihastarget=0:activate=0,state=1

;//Enemy at close range
:state=1,aitargetdistwithin=70:state=2
:state=2,varequal=0:state=60
:state=2,varequal=1:airotatetotarget,state=190
:state=2,aitargetdistfurther=70:state=1

;//melee strike
:state=190:state=191,etimerstart
:state=191:airotatetotarget
:state=191,aitargetdistwithin=70,aicanshoot=1:state=192,aistop
:state=191,aitargetdistfurther=70:state=1
:state=191,aicanshoot=0:state=1
:state=192:setaiactive=0,state=193,setframe=8
:state=193:incframe=8,airotatetotarget
:state=193,framebeyond=8 40,aitargetdistwithin=70:state=194,aiusemelee=1,sound=audiobank\misc\melee.wav
:state=193,frameatend=8:setframe=1,state=195
:state=194,frameatend=8:setframe=1,state=195
:state=194,etimergreater=1500:setframe=1,state=195
:state=195:setaiactive=1,aiattackawareness=0,aicleartarget,state=196
:state=196:aiattackawareness=1,state=1

;//Reloading: If not in combat, AI can readily reload.
:state=1,varequal=0,hasweapon=1,aiaction=1,ifweapon=0:state=10
:state=1,varequal=0,hasweapon=1,ifweapon=0,aihastarget=0,ducking=0:aistop,setaiactive=0,state=25
:state=1,varequal=0,hasweapon=1,ifweapon=0,aihastarget=0,ducking=1:aistop,setaiactive=0,state=45

;//Reloading: Assess situation-AI must make a tactical decision if needing to reload durring combat:  AI can choose to retreat and reload or charge foreward to melee range.
:state=10,aitargetdistwithin=70,rateoffire,random=1:state=190
:state=10,aitargetdistwithin=120,random=1:state=55
:state=10,aiatcover=0:aisettarget,state=20,etimerstart
:state=10,aiatcover=1,ducking=0:aistop,setaiactive=0,state=25
:state=10,aiatcover=1,ducking=1:aistop,setaiactive=0,state=45
:state=10,ducking=0:aistop,setaiactive=0,state=25
:state=10,ducking=1:aistop,setaiactive=0,state=45

;//Reloading: Attempt to fall back to cover and reload
:state=20,aiatcover=0:aimovetocover=0,airotatetotarget
:state=20,etimergreater=500,movingforwards=1:activate=1
:state=20,movingforwards=0:activate=0
:state=20,aiatcover=1,ducking=0:aistop,setaiactive=0,activate=0,state=25
:state=20,aiatcover=1,ducking=1:aistop,setaiactive=0,activate=0,state=45
:state=20,etimergreater=1000,ducking=0,random=2:aistop,setaiactive=0,activate=0,state=25
:state=20,etimergreater=1000,ducking=1,random=2:aistop,setaiactive=0,activate=0,state=45
:state=20,etimergreater=2000,idle=1,ducking=0:aistop,setaiactive=0,activate=0,state=25
:state=20,etimergreater=2000,idle=1,ducking=1:aistop,setaiactive=0,activate=0,state=45
:state=20,aitargetdistwithin=70,rateoffire,random=1:aistop,activate=0,state=191

;//Reload standing
:state=25:setframe=6,state=26
:state=26:incframe=6
:state=26,framebeyond=6 60:reloadweapon
:state=26,frameatend=6:sound=audiobank\guns\reload.wav,state=27
:state=27:state=1,setaiactive=1

;//Reload crouched
:state=45:setframe=36,state=46
:state=46:incframe=36
:state=46,framebeyond=36 60:reloadweapon
:state=46,frameatend=36:sound=audiobank\guns\reload.wav,state=47
:state=47:state=1,setaiactive=1

;//Give stand-down order when 'Z' is pressed (run main script)
;//:keypressed=44 1:aiattackawareness=0,aicleartarget,setaiactive=1,runfpidefault=1,sound=audiobank\switches\click.wav,state=999

;//Sit wounded and arrise as zombie if not healed-----------------------------------------------------------------------------------------------------------------------------------------
:always:localvar=1
:state=0:setvar=0
:varequal=0:aisetmeleedamage=10
:varequal=1:aisetmeleedamage=50,loopsound=audiobank\voices\monster\land.wav

:varequal=0,healthless=150:setvar=1,aistop,activate=0,addaiteam=6,setaiactive=0,state=992,setframe=11,alttexture=1

:state=992:incframe=11
:state=992,frameatend=11:state=997
:state=997:freeze,aistop,setframe=13
:state=997,random=10:state=998
:state=998:incframe=13
:state=998,frameatend=13:ainmate=1,aiattackawareness=0,aicleartarget,setaiactive=1,aiattackawareness=1,aisettarget,state=1

;//Start Script------------------------------------------------------------------------------------------------------------------------------------------------------------------------
:state=0:setaiactive=1,alwaysactive=1,reloadweapon,activate=0,aiattackawareness=1,etimerstart,state=1
;//End