;//Requires v1.17+ DarkAI
desc          = DarkAI generic shoot and melee for DAI arena demo  TEAM2

;//Dark AI Animations----------------------------------------------------------------------------------------------------
;//Running
:activated=1,ducking=0,movingforwards=1,aitargetdistfurther=200:animate=5
:activated=1,ducking=0,movingforwards=1,aitargetdistwithin=200:animate=2
;//Walking animations
:ducking=0,strafingleft=1:animate=3
:ducking=0,strafingright=1:animate=4
:activated=0,ducking=0,movingforwards=1:animate=2
:ducking=0,movingbackwards=1:animate=2
:ducking=0,idle=1:animate=1
;//Crouching Animations
:ducking=1,idle=0:animate=32
:ducking=1,idle=1:animate=31

;//Common behavior and triggers---------------------------------------------------------------------------------
;//Sprint when activated
:activated=0:aisetspeed=100
:activated=1:aisetspeed=250
:aihastarget=1,movingforwards=1,aitargetdistfurther=100:activate=1
:aihastarget=1,movingforwards=1,aitargetdistwithin=90:activate=0
:movingforwards=0:activate=0
:aihastarget=0:activate=0

;//Take cover when shot
:state=1,idle=1,shotdamage=1,random=1:state=60,aisettarget,aicallteam=500
:state=1,idle=0,shotdamage=1,random=10:state=60,aisettarget,aicallteam=500
:state=55,shotdamage=1,random=2:state=60,aisettarget,aicallteam=500
:state=30,shotdamage=1,random=1:state=60,aisettarget,aicallteam=500

;//Targeting
:state=1,aihastarget=0:aiattackawareness=1,aisettarget,airotatetotarget
:aihastarget=1,aicanshoot=0:aiattackawareness=0,setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=1:airotatetotarget

;//Shooting
:state=1,hasweapon=1,ifweapon=1,aicanshoot=1,idle=1,rateoffire:useweapon,rundecal=6
:state=1,hasweapon=1,ifweapon=1,aicanshoot=1,idle=0,rateoffire:useweapon,rundecal=6
:state=5,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6
:state=55,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6
:state=67,hasweapon=1,ifweapon=1,aicanshoot=1,rateoffire:useweapon,rundecal=6

;//Reloading: If not in combat, AI can readily reload.
:state=1,hasweapon=1,aiaction=1,ifweapon=0:state=10
:state=1,hasweapon=1,ifweapon=0,aihastarget=0,ducking=0:aistop,setaiactive=0,state=25
:state=1,hasweapon=1,ifweapon=0,aihastarget=0,ducking=1:aistop,setaiactive=0,state=45

;//Reloading: Assess situation-AI must make a tactical decision if needing to reload durring combat:  AI can choose to retreat and reload or charge foreward to melee range.
:state=10,aitargetdistwithin=70,rateoffire,random=1:state=190
:state=10,aitargetdistwithin=120,random=1:state=55
:state=10,aiatcover=0:aisettarget,state=20,etimerstart
:state=10,aiatcover=1,ducking=0:aistop,setaiactive=0,state=25
:state=10,aiatcover=1,ducking=1:aistop,setaiactive=0,state=45
:state=10,ducking=0:aistop,setaiactive=0,state=25
:state=10,ducking=1:aistop,setaiactive=0,state=45

;//Reloading: Attempt to fall back to cover and reload
:state=20,aiatcover=0:aimovetocover=0,airotatetotarget
:state=20,etimergreater=500,movingforwards=1:activate=1
:state=20,movingforwards=0:activate=0
:state=20,aiatcover=1,ducking=0:aistop,setaiactive=0,activate=0,state=25
:state=20,aiatcover=1,ducking=1:aistop,setaiactive=0,activate=0,state=45
:state=20,etimergreater=1000,ducking=0,random=2:aistop,setaiactive=0,activate=0,state=25
:state=20,etimergreater=1000,ducking=1,random=2:aistop,setaiactive=0,activate=0,state=45
:state=20,etimergreater=2000,idle=1,ducking=0:aistop,setaiactive=0,activate=0,state=25
:state=20,etimergreater=2000,idle=1,ducking=1:aistop,setaiactive=0,activate=0,state=45
:state=20,aitargetdistwithin=70,rateoffire,random=1:aistop,activate=0,state=191

;//Reload standing
:state=25:setframe=6,state=26
:state=26:incframe=6
:state=26,framebeyond=6 60:reloadweapon
:state=26,frameatend=6:sound=audiobank\guns\reload.wav,state=27
:state=27:state=1,setaiactive=1

;//Reload crouched
:state=45:setframe=36,state=46
:state=46:incframe=36
:state=46,framebeyond=36 60:reloadweapon
:state=46,frameatend=36:sound=audiobank\guns\reload.wav,state=47
:state=47:state=1,setaiactive=1

;//Respond to any sounds while not in combat
:state=1,idle=1,aiaction=0,aicanshoot=0,aiheardsound=2500:aicleartarget,state=30,etimerstart
:state=1,idle=0,aiaction=0,aicanshoot=0,aiheardsound=1000:aicleartarget,state=30,etimerstart
:state=30,varequal=1,aiheardsound=3000:airotatetosound
:state=30,varequal=0,aiheardsound=3000:aimovetosound
:state=30,etimergreater=4000:state=1
:state=30,aihastarget=0:aisettarget,airotatetotarget
:state=30,aihastarget=1,aicanshoot=0:aisettarget,airotatetotarget
:state=30,aicanshoot=1:state=1

;//Fall back to cover while returning fire
:state=60:etimerstart,state=67
:state=67,aiatcover=0:aimovetocover=0,airotatetotarget
:state=67,aiatcover=1:activate=0,state=1
:state=67,movingforwards=1,aitargetdistfurther=100,etimergreater=200:activate=1
:state=67,movingforwards=0:activate=0
:state=67,etimergreater=3000:activate=0,state=1
:state=67,etimergreater=2000,ifweapon=0:aistop,setaiactive=0,activate=0,state=25

;//close with enemy for melee if seen nearby
:state=1,aicanshoot=1,aitargetdistfurther=70,aitargetdistwithin=250,random=25:etimerstart,state=55
:state=55:aimovetotarget
:state=55,movingforwards=1,aitargetdistwithin=200,etimergreater=300:activate=1
:state=55,movingforwards=0:activate=0
:state=55,aitargetdistwithin=70:activate=0,state=190
:state=55,aitargetdistfurther=250:activate=0,state=1
:state=55,aicanshoot=0:activate=0,state=1
:state=55,aihastarget=0:activate=0,state=1

;//Enemy at close range
:state=1,aitargetdistwithin=70:state=2
:state=2,hasweapon=1,ifweapon=1,aicanshoot=1,random=5:state=60
:state=2,hasweapon=1,ifweapon=0,random=5:state=60
:state=2,hasweapon=1,ifweapon=1,random=1:airotatetotarget,state=190
:state=2,hasweapon=1,ifweapon=0:airotatetotarget,state=190
:state=2,aitargetdistfurther=70:state=1

;//melee strike
:state=190:state=191,etimerstart
:state=191:airotatetotarget
:state=191,aitargetdistwithin=70,aicanshoot=1:state=192,aistop
:state=191,aitargetdistfurther=70:state=1
:state=191,aicanshoot=0:state=1
:state=192:setaiactive=0,state=193,setframe=8
:state=193:incframe=8,airotatetotarget
:state=193,framebeyond=8 40,aitargetdistwithin=70:state=194,aisetmeleedamage=10,aiusemelee=1,sound=audiobank\misc\melee.wav
:state=193,frameatend=8:setframe=1,state=195
:state=194,frameatend=8:setframe=1,state=195
:state=194,etimergreater=1500:setframe=1,state=195
:state=195:setaiactive=1,state=1

;//Give stand-down order when 'Z' is pressed (run main script)
:keypressed=44 1:aiattackawareness=0,aicleartarget,setaiactive=1,runfpidefault=1,sound=audiobank\switches\click.wav,state=999

;//Sit wounded until dead or healed-----------------------------------------------------------------------------------------------------------------------------------------

:healthless=100:aistop,activate=0,addaiteam=7,setaiactive=0,state=776
:state=776:aicallteam=10000,state=777
:state=777:setframe=11,addaiteam=4,runfpi=DarkAI_Demo\DAI_117_Wounded_Team_2.fpi,state=999

;//Start Script------------------------------------------------------------------------------------------------------------------------------------------------------------------------
:state=0:setaiactive=1,alwaysactive=1,reloadweapon,activate=0,aiattackawareness=1,etimerstart,state=1
;//End